plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'pl.ros'
version = '0.0.1-SNAPSHOT'
description = 'penny-api'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
    withSourcesJar()  // Publishes sources for IDE navigation
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/laqrize/commons")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    mavenCentral()

}
ext {
    set('springCloudVersion', "2025.1.0")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation('pl.ros:commons:1.0.0-10') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-jpa'
    }
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    compileOnly 'jakarta.persistence:jakarta.persistence-api:3.2.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/laqrize/penny-api")
            credentials {
                username = "token"
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java

            pom {
                name = 'Penny API'
                description = 'Shared DTOs and API contracts for Keep microservices'
                url = 'https://github.com/laqrize/penny-api'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
            }
        }
    }
}